import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'java'
    id 'de.undercouch.download' version '3.1.1'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'me.kosinkadink'
version '0.2.0'

test {
    useJUnitPlatform()
}

repositories {
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "org.bstats:bstats-bukkit:1.8"
    compileOnly "org.spigotmc:spigot-api:"+project.property("apiversion")
    compileOnly "org.spigotmc:spigot:"+project.property("apiversion")
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly "me.clip:placeholderapi:2.10.5"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
    testCompileOnly "org.mockito:mockito-core:3.3.3"
    testCompile "com.googlecode.json-simple:json-simple:1.1.1"
    testCompile "org.spigotmc:spigot-api:"+project.property("apiversion")
    testCompile "org.spigotmc:spigot:"+project.property("apiversion")

    //compileOnly "com.fasterxml.jackson.core:jackson-core:2.11.0"
}

shadowJar {
    relocate 'org.bstats', 'me.kosinkadink.performantplants.metrics'
    archiveClassifier.set('')
}

compileJava.dependsOn {
    buildSpigot
}

// START Building Spigot and Bukkit

def spigotBuildDir = new File("$buildDir/spigot/")

task setupWorkspace(dependsOn: ['buildSpigot']) {
}

task buildSpigot(type: JavaExec) {
    if (hasSpigot()) {
        enabled = false;
        dependsOn = [];
    } else {
        dependsOn = ['cleanSpigotBuild', 'downloadBuildTool']
    }
    main = '-jar'
    args new File(spigotBuildDir, "BuildTools.jar").getAbsolutePath(), "--rev", project.property("apibuildtoolversion")
    workingDir = spigotBuildDir
}

task downloadBuildTool(type: Download) {
    spigotBuildDir.mkdirs()
    src project.property("buildtoolurl")
    dest new File(spigotBuildDir, "BuildTools.jar")
}

task cleanSpigotBuild() {
    spigotBuildDir.deleteDir()
}

def hasSpigot() {
    def groupId = "org.spigotmc"
    def version = project.property("apiversion")
    return hasArtifact(groupId, "spigot-api", version) && hasArtifact(groupId, "spigot", version)
}

def hasArtifact(groupId, artifactId, version) {
    def localMavenRepo = new File(new File(ant.properties['user.home'] as String), ".m2/repository/")
    def file = new File(localMavenRepo, groupId.replace('.', '/') + "/" + artifactId + "/" + version + "/")
    return file.exists()
}
